#!/usr/bin/zsh

app_name="Media Control"

function get_volume {
    pactl get-sink-volume @DEFAULT_SINK@ | grep -Po '[0-9]{1,3}(?=%)' | head -1
}

function get_mute {
    pactl get-sink-mute @DEFAULT_SINK@ | grep -Po '(?<=Mute: )(yes|no)'
}

function show_volume_notification {
    volume=$(get_volume)
    if [[ $(get_mute) = yes ]]; then
        label=muted
    else
        label=$volume%
    fi
    show_value_notification $volume "$(get_volume_icon) $label"
}

function get_volume_icon {
    volume=$(get_volume)
    if (( volume == 0 )) || [[ $(get_mute) = yes ]]; then
        echo 
    elif (( volume <= 50 )); then
        echo 
    else
        echo 
    fi
}

function get_brightness {
    absolute_brightness=$(brightnessctl get)
    max_brightness=$(brightnessctl max)
    absolute_brightness=$(( absolute_brightness * 100 ))
    echo $(( absolute_brightness / max_brightness ))
}

function show_brightness_notification {
    backlight=$(get_brightness)
    show_value_notification $backlight "$(get_brightness_icon) $backlight%"
}

function get_brightness_icon {
    brightness=$(get_brightness)
    if (( brightness <= 30 )); then
        echo 󰃞
    elif (( brightness <= 70 )); then
        echo 󰃟
    else
        echo 󰃠
    fi
}

function show_value_notification {
    notify-send --app-name $app_name --expire-time 2000 --hint string:x-dunst-stack-tag:$app_name --hint int:value:$1 $2
}

function clamp {
    if (( $1 <= $2 )); then
        echo $2
    elif (( $1 >= $3 )); then
        echo $3
    else
        echo $1
    fi
}

case $1 in
    volume)
        if [[ $2 = mute ]]; then
            pactl set-sink-mute @DEFAULT_SINK@ toggle
        else
            volume=$(get_volume)
            case $2 in
                up)
                    volume=$(( volume + 2 )) ;;
                down)
                    volume=$(( volume - 2 )) ;;
            esac
            volume=$(clamp $volume 0 100)
            pactl set-sink-volume @DEFAULT_SINK@ $volume%
        fi
        show_volume_notification
        ;;
    backlight)
        backlight=$(get_brightness)
        case $2 in
            up)
                if (( $backlight == 1 )); then
                    backlight=5
                else
                    backlight=$(( backlight + 5 ))
                fi
                ;;
            down)
                backlight=$(( backlight - 5 )) ;;
        esac
        backlight=$(clamp $backlight 1 100)
        brightnessctl set $backlight%
        show_brightness_notification
        ;;
esac
